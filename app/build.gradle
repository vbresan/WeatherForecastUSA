apply plugin: "com.android.application"

android {
    namespace "biz.binarysolutions.weatherusa"
    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "biz.binarysolutions.weatherusa"
        minSdkVersion 21
        targetSdk 34
        versionCode 27
        versionName "5.0"

        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt"
        }
    }

    flavorDimensions = ["distribution"]
    productFlavors {
        gplay {
            dimension "distribution"
        }
        fdroid {
            dimension "distribution"

            dependenciesInfo {
                includeInApk = false
                includeInBundle = false
            }
        }
    }
}

dependencies {
    implementation "com.github.bumptech.glide:glide:4.15.1"
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "com.google.android.material:material:1.12.0"

    // version 24.0.0+ requires min SDK 23
    gplayImplementation "com.google.android.gms:play-services-ads:23.6.0"
}

configurations.implementation {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
}
